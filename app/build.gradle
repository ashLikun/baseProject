plugins {
    id 'com.android.application'
    // 使用Kotlin插件
    id 'kotlin-android'
    //kapt插件
    id 'kotlin-kapt'
}
def config = rootProject.ext.android // 工程配置
def sdk = rootProject.ext.sdk // SDK配置
def libs = rootProject.ext.dependencies // 库依赖
def moudles = rootProject.ext.modules //全部组件
def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {

    signingConfigs {
        release {
        }
        debug {
        }
    }
    rootProject.ext.loadSigningConfigs(this)
    compileSdk config.compileSdk
    defaultConfig {
        applicationId config.applicationId
        minSdk config.minSdk
        targetSdk config.targetSdk
        versionCode config.versionCode
        versionName config.versionName
        flavorDimensions config.flavorDimensions
        signingConfig signingConfigs.release
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86"
        }
        // 适配默认语言"en-rUS"
        resConfigs "zh-rCN"
        multiDexEnabled true
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
        //向清单文件注入参数
        manifestPlaceholders = sdk.default
    }
    buildTypes {
        release {
            //自定义属性   BuildConfig.LOG_DEBUG
            // buildConfigField "boolean", "LOG_DEBUG", "false"
            //构建的时候打印文本
            println 'build - release'
            debuggable false
            //设置是否混淆
            minifyEnabled true
            //设置压缩对齐
            zipAlignEnabled true
            //移除无用的资源文件
            shrinkResources true
            //设置混淆配置文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //如果签名配置不为空 则设置签名信息
            if (signingConfigs.release != null) {
                signingConfig signingConfigs.release
            }
            //这里配置打包不同的参数
            manifestPlaceholders = sdk.releasBuild
        }
        debug {
            //自定义属性   BuildConfig.LOG_DEBUG
            //buildConfigField "boolean", "LOG_DEBUG", "false"
            //构建的时候打印文本
            println 'build - debug'
            debuggable true
            //设置是否混淆
            minifyEnabled false
            //设置压缩对齐
            zipAlignEnabled false
            //移除无用的资源文件
            shrinkResources false
            //设置混淆配置文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //versionNameSuffix
            //  versionNameSuffix "_内测"
            //applicationIdSuffix-测试版本使用debug作为后缀
            // applicationIdSuffix "debug"
            //这里配置打包不同的参数
            manifestPlaceholders = sdk.debugBuild
            if (signingConfigs.debug != null) {
                signingConfig signingConfigs.debug
            }

        }
    }
    /**
     * 指定签名包得输入文件名称
     */
    applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.isDebuggable()) {
                outputFileName = "App-v${variant.versionName}-${variant.buildType.name}-${releaseTime()}.apk"
            } else {
                outputFileName = "App-v${variant.versionName}-${variant.buildType.name}-${releaseTime()}.apk"
            }
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //公共组件
    implementation project(':component_common')
    kapt libs.aroutercompiler
    kapt libs.glideCompiler
    //主模块
    if (!moudles.main.isRun) {
        implementation project(':module_main')
    }
    //用户模块
    if (!moudles.user.isRun) {
        implementation project(':module_user')
    }
    //登录模块
    if (!moudles.login.isRun) {
        implementation project(':module_login')
    }
    //其他模块
    if (!moudles.other.isRun) {
        implementation project(':module_other')
    }

}
repositories {
    mavenCentral()
}
